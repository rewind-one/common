group 'one.rewind'
version '1.1.38-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// mainClassName = 'one.rewind.io.BasicRequester'

applicationDefaultJvmArgs = ["-Xms200m", "-Xmx4000m", "-Dsun.net.inetaddr.ttl=-1", "-Djava.net.preferIPv4Stack=true", "-Dlog4j.configurationFile=log4j2.xml", "-Dfile.encoding=UTF8", "-Djdk.http.auth.tunneling.disabledSchemes=\"\""]

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.1"
    }
}

repositories {

    jcenter()
    maven {
        url "http://repo.maven.apache.org/maven2"
    }
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "http://tfelab.org:60002/content/groups/public"
        credentials {
            username developer
            password developer_password
        }
    }
    flatDir {
        dirs 'lib'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            /*repository(url: 'http://tfelab.org:60002/content/repositories/releases/') {
                authentication(userName: deployment, password: deployment_password)
            }
            snapshotRepository(url: 'http://tfelab.org:60002/content/repositories/snapshots/') {
                authentication(userName: deployment, password: deployment_password)
            }*/
            repository(url: 'http://sdyk.red:315/artifactory/libs-release-local/') {
                authentication(userName: artifactory_username, password: artifactory_password)
            }
            snapshotRepository(url: 'http://sdyk.red:315/artifactory/libs-snapshot-local/') {
                authentication(userName: artifactory_username, password: artifactory_password)
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {

    contextUrl = 'http://sdyk.red:315/artifactory/'
    publish {
        repository {
            repoKey = 'libs-snapshot-local'
            username = artifactory_username
            password = artifactory_password
        }
        snapshotRepository {
            repoKey = 'libs-snapshot-local'
            username = artifactory_username
            password = artifactory_password
        }
        defaults {

            publications('mavenJava')

            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'linux', 'dev.team': 'core']
            publishPom = true
        }
    }
}

[distZip, distTar].each { task -> configurations.archives.artifacts.removeAll
        { it.class.simpleName == "ArchivePublishArtifact" && it.archiveTask == task }

    task.enabled = false
}

configurations {
    all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
    common
    compile.extendsFrom common
}

dependencies {

    // Google Guava
    compile 'com.google.guava:guava:24.1-jre'

    // Apache Commons Collections
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'

    // https://mvnrepository.com/artifact/it.sauronsoftware.cron4j/cron4j
    compile group: 'it.sauronsoftware.cron4j', name: 'cron4j', version: '2.2.5'

    // https://commons.apache.org/proper/commons-text/
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.3'

    // BrowserMob Proxy MITM 代理
    compile 'net.lightbody.bmp:browsermob-core:2.1.5'
    compile 'io.netty:netty-all:4.1.22.Final' // 强制更新netty

    // Appium java-client 自动化库
    //compile group: 'io.appium', name: 'java-client', version: '5.0.4'
    compile group: 'io.appium', name: 'java-client', version: '6.1.0'

    // ADB 连接
    compile 'com.github.vidstige:jadb:v1.0.1'

    // C3P0 数据库连接池
    compile 'com.mchange:c3p0:0.9.5.2'

    // ORM 数据库映射框架
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-jdbc:4.48'

    // Redisson Redis Java 分布式对象映射
    compile 'org.redisson:redisson:3.7.1'

    // MongoDB Java
    compile 'org.mongodb:mongo-java-driver:3.6.3'

    // 反射库
    compile 'org.reflections:reflections:0.9.11'

    // Jackson json
    compile 'com.fasterxml.jackson.core:jackson-core:2.8.11'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.11'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.11'

    // Typesafe Config
    compile 'com.typesafe:config:1.3.3'

    // ???
    compile 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'

    // https://mvnrepository.com/artifact/org.jsoup/jsoup TXT -> DOM
    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.2'

    // joda-time 线程安全时间类
    compile 'joda-time:joda-time:2.9.9'

    // SSH2 方法库
    compile 'ch.ethz.ganymed:ganymed-ssh2:build210'

    // SparkJava 快速HTTP接口服务
    compile 'com.sparkjava:spark-core:2.6.0'

    // 邮件服务方法
    compile 'javax.mail:mail:1.4.7'

    // OpenCV
    // compile name: 'opencv-341'
    // https://mvnrepository.com/artifact/org.bytedeco.javacpp-presets/opencv
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.4.1-1.4.1'

    // POI 读取Word Excel
    compile 'org.apache.poi:poi-ooxml:3.15'

    // https://mvnrepository.com/artifact/com.1stleg/jnativehook
    compile group: 'com.1stleg', name: 'jnativehook', version: '2.1.0'

    // SLF4J
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    // Log4J
    compile 'org.apache.logging.log4j:log4j-api:2.9.1'
    compile 'org.apache.logging.log4j:log4j-core:2.9.1'

    // JUnit
    testCompile group: 'junit', name: 'junit', version: '4.12'

    // MySQL
    runtime 'mysql:mysql-connector-java:5.1.37'
}

task copyFiles(group: 'tetra') << {
    copy {
        from 'chromedriver'
        into 'src/main/dist'
    }
    copy {
        from 'cacerts'
        into 'src/main/dist'
    }
    copy {
        from fileTree('src/main/resources')
        into 'src/main/dist'
    }
}

jar.dependsOn copyFiles